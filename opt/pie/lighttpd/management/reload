#!/usr/bin/env perl

use warnings;
use strict;

use CGI;
use IPC::Run qw/run timeout/;
use JSON;
use Try::Tiny;

$ENV{ 'PATH' } = '/usr/local/sbin:/usr/local/bin:/usr/sbin:/usr/bin:/sbin:/bin';

my $cgi = CGI->new;

my %data = (
  result    => 'unknown',
);
try {
  my ($output, $res);

  $res = run [qw#sudo pie-entrypoint.sh php5-fpm -t --fpm-config /etc/php5/fpm/php-fpm.conf#], '>&', \$output, timeout( 60 );
  $data{ 'configtest' } = $output;
  die "configtest failed" unless $res;

  if ($cgi->request_method eq 'POST') {
    $res = run [qw#sudo kill -USR2 1#], '>&', \$output, timeout( 60 );
    $data{ 'reload' } = $output;
    die "reload failed" unless $res;
  }

  $data{ 'result' } = 'success';
} catch {
  $data{ 'result' } = 'error';
  $data{ 'error' } = $_;
};

print $cgi->header(
  -type       => 'application/json',
  -status     => ($data{ 'result' } eq 'success' ? '200 OK' : '500 Internal Server Error'),
);
print encode_json( \%data );
